<!DOCTYPE html>
<!--[if lt IE 7 ]><html class="ie ie6" lang="en"><![endif]-->
<!--[if IE 7 ]><html class="ie ie7" lang="en"><![endif]-->
<!--[if IE 8 ]><html class="ie ie8" lang="en"><![endif]-->
<!--[if (gte IE 9)|!(IE)]><!--><html lang="en"><!--<![endif]-->
<head>
    <!-- Website Template designed by www.downloadwebsitetemplates.co.uk -->
    <!-- Modified to fit Cryogen.-->
    <meta charset="UTF-8">
    <title>The blog of Max Weber: Introducing db-view - Part 2</title>
    <meta name="description" content="">
    <meta name="keywords" content="">
    <meta content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=no" name="viewport">
    <link rel="apple-touch-icon-precomposed" sizes="144x144" href="images/ico/apple-touch-icon-144.png">
    <link rel="apple-touch-icon-precomposed" sizes="114x114" href="images/ico/apple-touch-icon-114.png">
    <link rel="apple-touch-icon-precomposed" sizes="72x72" href="images/ico/apple-touch-icon-72.png">
    <link rel="apple-touch-icon-precomposed" href="images/ico/apple-touch-icon-57.png">
    <link rel="shortcut icon" href="images/ico/favicon.png">
    <!--[if IE]><![endif]-->
    <link href="/css/buttons.css" rel="stylesheet" type="text/css" />
    <link href="/css/menu.css" rel="stylesheet" type="text/css" />
    <link href="/css/reset.css" rel="stylesheet" type="text/css" />
    <link href="/css/style.css" rel="stylesheet" type="text/css" />
    <link href="/css/typography.css" rel="stylesheet" type="text/css" />
    <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.2.0/css/font-awesome.min.css" rel="stylesheet">
    <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/tomorrow-night-eighties.min.css">
    <!--[if lt IE 9]><script src="http://html5shiv.googlecode.com/svn/trunk/html5.js"></script><![endif]-->
</head>
<body>

<div id="left">

    <p id="logo">
        <a title="The blog of Max Weber" href="/">
          <img src="/img/profile.jpg"></img>
          <span class="text">Max Weber</span>
        </a>
    </p>

    <div id="menucont" class="bodycontainer clearfix">
        <div class="menutitle">
            <p><span class="fa fa-reorder"></span><strong>Menu</strong></p>
        </div>
        <ul class="menu">
            <li ><a title="Home" href="/">Home</a></li>
            <li ><a title="Archives" href="/archives">Archives</a></li>
            
            <li >
                <a href="/pages/about">About</a>
            </li>
            
            <li><a title="RSS" href="/feed.xml">RSS</a></li>
        </ul>
    </div>

    <div id="socialmedia" class="clearfix">
        <ul>
            <li><a title="GitHub" href="https://github.com/maxweber/" rel="external"><span class="fa fa-github"></span></a></li>
            <li><a title="Twitter" href="https://twitter.com/webermaximilian" rel="external"><span class="fa fa-twitter"></span></a></li>
        </ul>
    </div>

</div>

<div id="right" class="clearfix">
    
<div id="post">
    <div class="post-header">
    <div id="post-meta" class="row">
        <strong>5. August 2019</strong>
        
    </div>
    <h1>Introducing db-view - Part 2</h1>
</div>
<div>
    
    <p>In <a href='https://maxweber.github.io/blog/2019-07-25-introducing-db-view-part-1'>part 1</a> I've talked about the db-view approach that has different trade-offs in comparison to a REST API. In essence you decide to develop your backend API endpoint with a single frontend app in mind. Thereby your API endpoint and frontend tends to stay <a href='https://www.infoq.com/presentations/Simple-Made-Easy/'>simple</a>.</p><p>This part is about the example app that I've developed for this blog post series. It implements the well-known <a href='http://todomvc.com/'>TodoMVC</a> example, an app to manage todo items. In comparison to many other TodoMVC implementations out there, it implements the frontend and the backend. It is a core principle of the db-view approach that you design your API exclusively for one frontend app, instead of providing a more generic API. The latter one introduces <a href='https://maxweber.github.io/blog/2019-06-15-approaching-the-web-after-tomorrow-part-3'>various complexities</a> which needs to be solved, despite the fact that they are not strictly required for your app.</p><p>The source code for the example app can be found here:</p><p><a href='https://github.com/maxweber/todomvc-db-view'>https://github.com/maxweber/todomvc-db-view</a></p><p>It uses Clojure for the backend and ClojureScript for the frontend. The <a href='https://leiningen.org/'>Leiningen</a> build tool is used for the Clojure part, while <a href='https://github.com/thheller/shadow-cljs'>shadow-cljs</a> is used for the frontend. The example <a href='https://github.com/maxweber/todomvc-db-view/blob/master/README.md'>README</a> describes how to start the app and the required tools.</p><p>The API endpoint <code>/db-view/get</code> is used to receive the <code>:db-view/output</code> map from the server. This is the corresponding frontend code:</p><pre><code class="clojure">&#40;ns todomvc-db-view.db-view.get
  &#40;:require &#91;cljs.core.async :as a&#93;
            &#91;cljs-http.client :as http&#93;
            &#91;todomvc-db-view.state.core :as state&#93;&#41;
  &#40;:require-macros &#91;cljs.core.async.macros :refer &#91;go&#93;&#93;&#41;&#41;

;; Concept:
;;
;; Gets and refreshes the db-view from the server. Provides the server
;; API endpoint '/db-view/get' with the `:db-view/input` map from the
;; app state. Receives the `:db-view/output` map in the API response
;; and stores it in the app state. Reagent will trigger a rerender of
;; all UI components which depend on changed parts of the
;; `:db-view/output`.

&#40;defn get-view
  &quot;Provides the server API endpoint '/db-view/get' with the
   `:db-view/input` map from the app state and returns the API response
   that contains the `:db-view/output` map.&quot;
  &#91;state-value&#93;
  &#40;go
    &#40;let &#91;response &#40;&lt;! &#40;http/request
                        {:request-method :post
                         :url &quot;/db-view/get&quot;
                         ;; NOTE: for a production app add
                         ;;       authorization here and prefer the
                         ;;       Transit format:
                         :edn-params &#40;:db-view/input state-value&#41;}&#41;&#41;&#93;
      &#40;:body response&#41;&#41;&#41;&#41;

&#40;defn refresh!
  &quot;Provides the server API endpoint '/db-view/get' with the
   `:db-view/input` map from the app state, receives the
   `:db-view/output` map from the API response and stores it in the app
   state.&quot;
  &#91;&#93;
  &#40;go
    &#40;:db-view/output
     &#40;swap! state/state
            assoc
            :db-view/output
            &#40;&lt;! &#40;get-view @state/state&#41;&#41;&#41;&#41;&#41;&#41;
</code></pre><p>In the recent years I tend to use a light form of <a href='https://en.wikipedia.org/wiki/Literate_programming'>Literate programming</a>, where you provide more explanation of the logic in a natural language. Therefore you find the explanation of the source code in the 'Concept' header, the comments and the doc-strings of the functions above.</p><p>The app state is hold in a <a href='https://github.com/reagent-project/reagent'>Reagent</a> atom:</p><pre><code class="clojure">&#40;ns todomvc-db-view.state.core
  &#40;:require &#91;reagent.core :as r&#93;&#41;&#41;

;; Concept:
;;
;; Namespace with the Reagent atom `state` that holds the global app
;; state of this ClojureScript app.

&#40;def initial-state-value
  {:db-view/input {:todo/list {}}}&#41;

&#40;defonce state
  &#40;r/atom initial-state-value&#41;&#41;
</code></pre><p>The counterpart for the <code>/db-view/get</code> API endpoint on the server-side looks like this:</p><pre><code class="clojure">&#40;ns todomvc-db-view.db-view.get
  &#40;:require &#91;datomic.api :as d&#93;
            &#91;todomvc-db-view.util.edn :as edn&#93;
            &#91;todomvc-db-view.db-view.todo-list :as todo-list&#93;
            &#91;todomvc-db-view.db-view.todo-edit :as todo-edit&#93;
            &#91;todomvc-db-view.db-view.todo-new :as todo-new&#93;&#41;&#41;

;; Concept:
;;
;; Provides the API endpoint to get the db-view. The request body
;; contains the parameters to query the database to assemble the
;; `:db-view/output` map, that contains the required data for the
;; current active UI parts. This value is returned in the response
;; body and the client stores it in the Reagent app state atom, where
;; the UI components can access it.

&#40;defn get-view
  &quot;Main entry point to gather the `:db-view/output` map for the
   client. Based on the given Datomic database value `db` and the
   `:db-view/input` map from the client.&quot;
  &#91;db db-view-input&#93;
  &#40;merge
   &#40;todo-list/get-view db
                       db-view-input&#41;
   &#40;todo-edit/get-view db
                       db-view-input&#41;
   &#40;todo-new/get-view db
                      db-view-input&#41;
   ;; NOTE: add other db-view parts here.
   &#41;&#41;

&#40;defn ring-handler
  &quot;Ring handler to get the `:db-view/output` map for the given
   `:db-view/input` map in the `request` body.&quot;
  &#91;db request&#93;
  &#40;when &#40;and &#40;= &#40;:request-method request&#41; :post&#41;
             &#40;= &#40;:uri request&#41; &quot;/db-view/get&quot;&#41;&#41;
    ;; NOTE: for a production app rather use
    ;;       &#91;Transit&#93;&#40;https://github.com/cognitect/transit-format&#41;
    ;;       here instead of EDN:
    &#40;let &#91;db-view-input &#40;edn/read-string &#40;slurp &#40;:body request&#41;&#41;&#41;&#93;
      ;; NOTE: for a production app do the appropriate authorization
      ;;       checks:
      &#40;edn/response
       &#40;get-view db
                 db-view-input&#41;&#41;&#41;&#41;&#41;
</code></pre><p>I already included the commands into the example app, which are used to perform side-effects like adding a new todo-item. Commands will be explained in detail in the next blog post. Now let's have a look at the namespace that contains the db-view implementation for the todo list UI:</p><pre><code class="clojure">&#40;ns todomvc-db-view.db-view.todo-list
  &#40;:require &#91;datomic.api :as d&#93;&#41;&#41;

;; Concept:
;;
;; The db-view part for the todo list UI.

&#40;defn q-all
  &quot;Queries all todo-item entities and pulls the attributes which are
   required for todo list UI.&quot;
  &#91;db&#93;
  &#40;d/q
   '&#91;:find
     &#91;&#40;pull ?e
            &#91;:db/id :todo/title :todo/done&#93;&#41; ...&#93;
     :where
     &#91;?e :todo/title&#93;&#93;
   db&#41;&#41;

&#40;defn get-view
  &quot;Returns the db-view for the todo list UI.&quot;
  &#91;db db-view-input&#93;
  &#40;when-let &#91;params &#40;:todo/list db-view-input&#41;&#93;
    &#40;let &#91;all &#40;q-all db&#41;
          {:keys &#91;active completed&#93;} &#40;group-by &#40;fn &#91;todo-item&#93;
                                                 &#40;if &#40;:todo/done todo-item&#41;
                                                   :completed
                                                   :active&#41;&#41;
                                               all&#41;
          todo-items &#40;case &#40;:todo/filter params&#41;
                       :active
                       active
                       :completed
                       completed
                       all&#41;&#93;
      {:todo/list {:todo/list-items todo-items
                   :todo/active-count &#40;count active&#41;
                   :todo/completed-count &#40;count completed&#41;}}&#41;&#41;&#41;
</code></pre><p>In the code above I removed the command-related parts for the purpose of this blog post, but you can already find it in the <a href='https://github.com/maxweber/todomvc-db-view/blob/master/src/clj/todomvc_db_view/db_view/todo_list.clj'>Github repo</a>. Altogether the code is really straightforward:</p><ul><li>it runs a Datomic query that gets all todo-items with the attributes  required for the todo list UI</li><li>groups the query results into active and completed todo-items</li><li>returns the group that was requested via the <code>:todo/filter</code> parameter as <code>:todo/list-items</code></li><li>counts the active and completed todo-items (this information is used  in the bottom bar of the todo list UI)</li></ul><p>The notify part of the app enables the real-time or rather auto-update behaviour of the example app. To try it out, just open the example app in two browser windows, then you see that changes in one window also get replicated in the other window. On the client side the code looks like this:</p><pre><code class="clojure">&#40;ns todomvc-db-view.db-view.notify
  &#40;:require &#91;cljs-http.client :as http&#93;
            &#91;todomvc-db-view.db-view.get :as get&#93;
            &#91;cljs.core.async :as a&#93;&#41;
  &#40;:require-macros &#91;cljs.core.async.macros :refer &#91;go-loop&#93;&#93;&#41;&#41;

;; Concept:
;;
;; Listens for changes in the Datomic transaction log, which affect
;; the current logged-in user. Uses HTTP long polling to allow the API
;; endpoint on the server to push a message. The server will notify
;; the client everytime, when a transaction contains something
;; relevant for the current logged-in user.
;;
;; Due to this mechanism the client can show updates, which were issued
;; by the server &#40;a finished background job for example or a change by
;; another user&#41;.

&#40;defn start-listening
  &quot;Starts a go-loop that opens a long-polling request to the
   '/db-view/notify' API endpoint. Refreshes the `:db-view/output` map
   in the app state, when it receives a HTTP 200 response. Sleeps for a
   short moment, when it receives an error response to not DDoS the
   server in the case of a server issue.&quot;
  &#91;&#93;
  &#40;go-loop &#91;&#93;
    &#40;let &#91;response &#40;&lt;! &#40;http/request
                         {:request-method :post
                          :url &quot;/db-view/notify&quot;}&#41;&#41;&#93;
      &#40;if &#40;= &#40;:status response&#41;
             200&#41;
        &#40;&lt;! &#40;get/refresh!&#41;&#41;
        &#40;&lt;! &#40;a/timeout 2000&#41;&#41;&#41;
      &#40;recur&#41;&#41;&#41;&#41;
</code></pre><p>The server counterpart looks like this:</p><pre><code class="clojure">&#40;ns todomvc-db-view.db-view.notify
  &#40;:require &#91;org.httpkit.server :as httpkit&#93;
            &#91;datomic.api :as d&#93;
            &#91;todomvc-db-view.util.edn :as edn&#93;&#41;&#41;

;; Concept:
;;
;; Provides an API endpoint that allows clients to listen for changes
;; in the Datomic transaction log, which affect their current
;; logged-in user.
;;
;; Thereby the client can refresh the `:db-view/output` map as soon as
;; it is notified by this API endpoint. HTTP long polling is used here
;; to allow the server to push a message to the client. It is less
;; complex to implement in comparison to WebSockets. Furthermore the
;; low latency and reduced payload size of WebSockets is not required
;; for this use case.

&#40;defonce client-listeners-state
  ;; holds the httpkit channels of the clients, which are waiting for
  ;; a db-view notify:
  &#40;atom {}&#41;&#41;

&#40;defn ring-handler
  &quot;Ring-handler for the '/db-view/notify' API endpoint.&quot;
  &#91;request&#93;
  &#40;when &#40;and &#40;= &#40;:request-method request&#41; :post&#41;
             &#40;= &#40;:uri request&#41; &quot;/db-view/notify&quot;&#41;&#41;
    ;; NOTE: for a production app add an authentication check here:
    &#40;httpkit/with-channel request channel
      ;; register the user's browser session in the
      ;; `client-listeners-state`:
      &#40;swap! client-listeners-state
             assoc
             channel
             channel&#41;
      &#40;httpkit/on-close channel
                        &#40;fn &#91;status&#93;
                          ;; remove the user's browser session from
                          ;; the `client-listeners-state` as soon as
                          ;; the channel is closed, meaning the user
                          ;; has closed the browser tab or the network
                          ;; connection was interrupted:
                          &#40;swap! client-listeners-state
                                 dissoc
                                 channel&#41;&#41;&#41;&#41;&#41;&#41;

&#40;defn notify
  &quot;A Datomic transaction listener that notifies all user browser
   sessions, where the user was affected by the transaction of the
   `tx-report`.&quot;
  &#91;tx-report&#93;
  &#40;let &#91;basis-t &#40;d/basis-t &#40;:db-after tx-report&#41;&#41;
        response &#40;edn/response
                  {:db/basis-t basis-t}&#41;&#93;
    ;; NOTE: for a production app only send notifications to the users
    ;;       which are affected by this `tx-report`:
    &#40;doseq &#91;channel &#40;vals @client-listeners-state&#41;&#93;
      &#40;httpkit/send! channel
                     response&#41;&#41;&#41;&#41;
</code></pre><p>As already mentioned in the note above you would normally only notify the connected users (client-listeners) which are affected by the current Datomic transaction report. For example if your app supports teams and one user makes a change on the team's todo list, only the team mates which are currently online would be notified.</p><p>Datomic has this great feature that you can access the transaction log and even receive push updates of new transactions via the <a href='https://docs.datomic.com/on-prem/clojure/index.html#datomic.api/tx-report-queue'><code>datomic.api/tx-report-queue</code></a>. Most likely you will have multiple web servers, which response to db-view requests and executes commands (which transacts Datomic transactions). With the help of Datomic's transaction queue it does not matter to which server the client has opened the long-polling HTTP request, since all servers will be informed of the new transaction and initiate the necessary notification for an affected user.</p><p>The db-view approach also works for other databases than Datomic. But most of them do not provide access to a transaction queue. A do-it-yourself alternative to push the notifications is an effort that should not be underestimated. Therefore I would recommend services like <a href='https://pusher.com/'>Pusher</a> or <a href='https://www.pubnub.com/'>Pubnub</a>, where you can push messages to the user's web browser, without building the required infrastructure yourself. The server would then send the notifications to the affected users right after it has transacted the corresponding Datomic transaction.</p><p>As already mentioned, the next blog post will cover the concept of commands in the context of a db-view application.</p>
</div>

<div id="post-tags">
    <br/> 
    <b>Tags: </b>
    
    <a href="/tags/Clojure">Clojure</a>
    
    <a href="/tags/ClojureScript">ClojureScript</a>
    
    <a href="/tags/db-view">db-view</a>
    
    <a href="/tags/Datomic">Datomic</a>
    
</div>

<br/>

    <div id="prev-next">
        
        
        <a class="right button" href="/blog/2019-07-25-introducing-db-view-part-1">Introducing db-view - Part 1 &raquo;</a>
        
    </div>

    


</div>

<hr/>
<div id="footercont" class="clearfix">Copyright &copy; 2019 Max Weber
    <p>Powered by <a href="http://cryogenweb.org">Cryogen</a> | Free Website Template by <a title="free website templates" href="http://www.downloadwebsitetemplates.co.uk" rel="external">Download Website Templates</a></p>

</div>
</div>
<script src="//code.jquery.com/jquery-1.11.0.min.js"></script>
<script src="/js/highlight.pack.js" type="text/javascript"></script>
<script>hljs.initHighlightingOnLoad();</script>
<script src="/js/scripts.js" type="text/javascript"></script>


</body>
</html>
